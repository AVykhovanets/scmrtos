//******************************************************************************
//*
//*     FULLNAME:  Single-Chip Microcontroller Real-Time Operating System
//*
//*     NICKNAME:  scmRTOS
//*
//*     PROCESSOR: MSP430 (Texas Instruments)
//*
//*     TOOLKIT:   EW430 (IAR Systems)
//*               
//*     PURPOSE:   Target Dependent Low-Level Stuff
//*               
//*     Version:   3.00-beta
//*
//*     $Revision$
//*     $Date$
//*
//*     Copyright (c) 2003-2006, Harry E. Zhurov
//*
//*     Permission is hereby granted, free of charge, to any person 
//*     obtaining  a copy of this software and associated documentation 
//*     files (the "Software"), to deal in the Software without restriction, 
//*     including without limitation the rights to use, copy, modify, merge, 
//*     publish, distribute, sublicense, and/or sell copies of the Software, 
//*     and to permit persons to whom the Software is furnished to do so, 
//*     subject to the following conditions:
//*
//*     The above copyright notice and this permission notice shall be included 
//*     in all copies or substantial portions of the Software.
//*
//*     THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, 
//*     EXPRESS  OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF 
//*     MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. 
//*     IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY 
//*     CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, 
//*     TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH 
//*     THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//*
//*     =================================================================
//*     See http://scmrtos.sourceforge.net for documentation, latest
//*     information, license and contact details.
//*     =================================================================
//*
//******************************************************************************

#include "scmRTOS_CONFIG.h"
#include "scmRTOS_TARGET_CFG.h"

     module  scmRTOS_Asm

;-------------------------------------------------------------------------------
save_regs   macro

     push     r4
     push     r5
     push     r6
     push     r7
     push     r8
     push     r9
     push     r10
     push     r11
     push     r12
     push     r13
     push     r14
     push     r15

     endm
;-------------------------------------------------------------------------------
restore_regs   macro

     pop      r15
     pop      r14
     pop      r13
     pop      r12
     pop      r11
     pop      r10
     pop      r9
     pop      r8
     pop      r7
     pop      r6
     pop      r5
     pop      r4

     endm
;-------------------------------------------------------------------------------
   
     public  OS_Start

#if scmRTOS_CONTEXT_SWITCH_SCHEME == 0
    public  OS_ContextSwitcher
#endif 

     rseg  DATA16_Z(1)
     rseg  CODE(1)

;-------------------------------------------------------------------------------
OS_Start:
     mov.w  r12,SP
     jmp    L_RestoreContext

;-------------------------------------------------------------------------------
#if scmRTOS_CONTEXT_SWITCH_SCHEME == 0
OS_ContextSwitcher:
     push SR
     save_regs

     mov.w   SP,0(r12)
     mov.w   r14,SP

L_RestoreContext:
     restore_regs
     reti
#endif 
;-------------------------------------------------------------------------------
#if scmRTOS_CONTEXT_SWITCH_SCHEME == 1

    extern  OS_ContextSwitchStackData
    extern  OS_ContextSwitchHook

    common INTVEC(1)
    org   CONTEXT_SWITCH_ISR_VECTOR
    DC16  ContextSwitcher_ISR

    rseg CODE(1)

ContextSwitcher_ISR:
    save_regs
    mov   SP,r12
    call  #OS_ContextSwitchHook
    mov   r12,SP
L_RestoreContext:
    restore_regs
    reti

#endif // scmRTOS_CONTEXT_SWITCH_SCHEME
;-------------------------------------------------------------------------------

     end
;*******************************************************************************
